resource "aws_instance" "instance" {
  key_name = var.key_name

  count = length(var.instances)

  ami           = var.instances[count.index].ami
  instance_type = var.instances[count.index].instance_type

  associate_public_ip_address = var.instances[count.index].public_ip
  subnet_id                   = var.instances[count.index].subnet_id
  vpc_security_group_ids      = [aws_security_group.ping.id, aws_security_group.security_group[count.index].id]
  private_ip                  = var.instances[count.index].private_ip

  tags = {
    Name = "${var.name}-${var.instances[count.index].name}"
  }
}

resource "aws_security_group" "security_group" {
  count = length(var.instances)

  name        = "${var.instances[count.index].name}-security_group"
  description = "The security group for ${var.instances[count.index].name} generated by Terraform"

  vpc_id = var.vpc_id

  dynamic "ingress" {
    for_each = var.instances[count.index].ports

    content {
      from_port   = ingress.value.port
      to_port     = ingress.value.port
      protocol    = ingress.value.protocol
      cidr_blocks = ingress.value.cidr_blocks
    }
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${var.instances[count.index].name}-security_group"
  }
}

resource "aws_security_group" "ping" {
  name        = "${var.name}-icmp-security_group"
  description = "The security group that allows pings (icmp)"

  vpc_id = var.vpc_id

  ingress {
    from_port   = -1
    to_port     = -1
    protocol    = "icmp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${var.name}-icmp-security_group"
  }
}
